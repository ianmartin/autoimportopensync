dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.58)
AC_INIT(configure.in)
AM_INIT_AUTOMAKE(opensync, 0.06)
dnl AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_DISABLE_STATIC
dnl AC_DISABLE_SHARED
AC_PROG_LIBTOOL
AC_PROG_YACC

PKG_CHECK_MODULES(XML, [libxml-2.0])
AC_SUBST(XML_CFLAGS)
AC_SUBST(XML_LIBS)

AM_PATH_PYTHON(2.2)

OPENSYNC_CONFIGDIR=${datadir}/opensync
AC_SUBST(OPENSYNC_CONFIGDIR)

OPENSYNC_PLUGINDIR=${libdir}/opensync
AC_SUBST(OPENSYNC_PLUGINDIR)

OPENSYNC_FORMATSDIR=${libdir}/opensync/formats
AC_SUBST(OPENSYNC_FORMATSDIR)

AC_CHECK_HEADER(check.h,HAVE_CHECK=1,HAVE_CHECK=0)
AM_CONDITIONAL(ENABLE_TESTS, test x$HAVE_CHECK = x1)
if test "x${HAVE_CHECK}" = "x1"; then
	AM_PATH_CHECK()
	testdir="tests"
else
	testdir=""
fi
AC_SUBST(testdir)

AC_ARG_ENABLE(engine,[  --enable-engine        enable building of the opensync engine],ENABLE_ENGINE=$enableval,ENABLE_ENGINE="yes")
if test "x${ENABLE_ENGINE}" = "xyes"; then
	AC_DEFINE(BUILD_ENGINE,1,[Stress Testing])
	enginedir="engine"
else
	enginedir=""
fi
AC_SUBST(enginedir)

dnl AX_PATH_BDB([4.2],[LDFLAGS="$LDFLAGS $BDB_LDFLAGS"
dnl LIBS="$LIBS $BDB_LIBS"
dnl CPPFLAGS="$CPPFLAGS $BDB_CPPFLAGS"],AC_MSG_ERROR(You must have libdb of version 4.1 or higher installed))

AC_CACHE_CHECK([for $am_display_PYTHON version], [am_cv_python_version],
	[am_cv_python_version=`$PYTHON -c "import sys; print sys.version[[:3]]"`])
AC_SUBST([PYTHON_VERSION], [$am_cv_python_version])

AC_CHECK_PROGS(PYREX, pyrexc)

py_prefix=`$PYTHON -c "import sys; print sys.prefix"`
py_exec_prefix=`$PYTHON -c "import sys; print sys.exec_prefix"`
PYTHON_INCLUDES="-I${py_prefix}/include/python${PYTHON_VERSION}"
if test "$py_prefix" != "$py_exec_prefix"; then
  PYTHON_INCLUDES="$PYTHON_INCLUDES -I${py_exec_prefix}/include/python${PYTHON_VERSION}"
fi
AC_SUBST(PYTHON_INCLUDES)

pkg_modules="glib-2.0 gmodule-2.0 gobject-2.0 gthread-2.0 sqlite3"
PKG_CHECK_MODULES(PACKAGE, [$pkg_modules])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

AC_OUTPUT([
Makefile
src/Makefile
tools/Makefile
tests/Makefile
formats/Makefile
formats/xml/Makefile
engine/Makefile
])

echo "================"
if test "x${HAVE_CHECK}" = "x1"; then
	echo "TESTS are enabled."
else
	echo "TESTS are disabled."
fi
if test "x${ENABLE_ENGINE}" = "xyes"; then
	echo "The opensync engine will be built."
else
	echo "The opensync engine will NOT be built."
fi
echo
echo "Now build opensync and install it"
echo "Afterwards go into the plugins directory"
echo "and build the plugins you need."
echo "================"
